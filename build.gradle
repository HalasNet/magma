apply plugin: 'idea'
apply from: 'libraries.gradle'

ext {
  javaVersion = '1.6'
}

allprojects {

  apply plugin: 'java'
  apply plugin: 'maven'

  group = 'org.obiba.magma'
  version = '1.6-gradle-SNAPSHOT'

  sourceCompatibility = javaVersion
  targetCompatibility = javaVersion

  defaultTasks 'build'

  buildDir = 'target'

  repositories {
    mavenLocal()
    maven {
      url 'http://repo.obiba.org'
    }
    mavenCentral()
  }

  tasks.withType(Compile) {
    options.encoding = 'UTF-8'
  }

  configurations {
    pmd
    compile
    runtime
    all*.exclude group: 'commons-logging'
  }

}

task wrapper(type: Wrapper) {
  gradleVersion = '1.5'
}

subprojects {

  task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  apply plugin: 'project-report'
  reporting {
    baseDir "$buildDir/reports"
  }

  artifacts.archives packageSources

  dependencies {
    compile(libraries.guava)
    compile(libraries.slf4j_api)
    compile(libraries.jsr305)
    compile(libraries.findbugs_annotation)

    testCompile(libraries.junit)
    testCompile(libraries.easymock)
    testCompile(libraries.hamcrest)
    testCompile(libraries.slf4j_log4j12)
    testCompile(libraries.jcl_slf4j)

    pmd 'net.sourceforge.pmd:pmd:5.0.3'
  }

  apply plugin: 'findbugs'
  findbugs {
    reportsDir = file("${reporting.baseDir}/findbugs-output")
    excludeFilter = file(rootProject.file('src/main/resources/findbugs-exclude.xml'))
    ignoreFailures = true
  }


  task pmd << {
    file("${reporting.baseDir}").mkdirs()
    ant {
      taskdef(name: 'pmd', classname: 'net.sourceforge.pmd.ant.PMDTask', classpath: configurations.pmd.asPath)
      pmd(shortFilenames: 'true', failonruleviolation: 'false', rulesetfiles: rootProject.file('src/main/resources/pmd-ruleset.xml').toURI().toString()) {
        formatter(type: 'html', toFile: "${reporting.baseDir}/pmd.html")
        formatter(type: 'xml', toFile: "${reporting.baseDir}/pmd.xml")
        fileset(dir: 'src') {
          include(name: '**/*.java')
        }
      }
    }
  }

}
